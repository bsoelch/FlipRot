#_ Stack implementation at the upper end of RAM
Memory layout: 
0xffffff-0xfffffd addresses for tmp-vars
0xfffffc-0xfffff1 addresses additional register-like values
0xfffff0 stack-pointer
0xffffef-0xf00000 stack 
_#

#_ addresses for temporary values within macros/procedures, 
these address may be overwritten by any macro _#
#def tmp_ret 0xffffff #enddef
#def tmp_loop    0xfffffe #enddef
#_ temporay value that is not modified by the push or pop _#
#def tmp_val    0xfffffd #enddef

#_ addresses for immediate values 
this addresses may be saved on/restored from the stack on procedure calls
_#
#def varC 0xfffffc #enddef
#def varB 0xfffffb #enddef
#def varA 0xfffffa #enddef
#def var9 0xfffff9 #enddef
#def var8 0xfffff8 #enddef
#def var7 0xfffff7 #enddef
#def var6 0xfffff6 #enddef
#def var5 0xfffff5 #enddef
#def var4 0xfffff4 #enddef
#def var3 0xfffff3 #enddef
#def var2 0xfffff2 #enddef
#def var1 0xfffff1 #enddef

#_ points to address above the top of the stack _#
#def stack_prt 0xfffff0 #enddef
#_ resets the stack, 
this macro should be used before the first call of push _#
#def stack_reset
stack_prt swap 0xffffef store
#enddef

#_ skip code in file _#
#undef $stack$.skipCode
$stack$.skipCode swap 1 jumpif

#_ increments the stack_prt
	modifies tmp_loop, tmp_ret 
_#
#label stack_prt_inc
#undef stack_prt_inc.loop
#undef stack_prt_inc.break
#undef stack_prt_inc.reset
tmp_ret swap store #_ save return address _#
#_ set tmp_loop (loop-count) to unary 20 _#
tmp_loop swap 0xfffff store 
#label stack_prt_inc.loop
stack_prt swap stack_prt load flip store #_ flip lowest bit of stack_prt _#
swap stack_prt_inc.break swap
jumpif
swap stack_prt swap rot store
tmp_loop swap tmp_loop load rot store
swap stack_prt_inc.loop swap
jumpif #_ jump to loop if rotation count not yet reached _#
swap stack_prt_inc.reset swap 
1 jumpif #_ jump to reset _#
#label stack_prt_inc.break
stack_prt swap stack_prt load rot store
tmp_loop swap tmp_loop load rot store
swap stack_prt_inc.break swap
jumpif #_ jump to break if rotation count not yet reached _#
swap stack_prt_inc.reset swap 
1 jumpif #_ jump to reset _#
#label stack_prt_inc.reset
stack_prt swap stack_prt load
#_ rotate by 64-20 _#
rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot 
rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot 
rot rot rot rot rot rot rot rot rot rot rot rot   
store 
#undef stack_prt_inc.loop
#undef stack_prt_inc.break
#undef stack_prt_inc.reset
tmp_ret load swap #_ load return address _#
1 jumpif
#_ end of stack_prt_int _#

#_ decrements the stack_prt, 
	modifies tmp_loop, tmp_ret 
_#
#label stack_prt_dec
#undef stack_prt_dec.loop1
#undef stack_prt_dec.break
#undef stack_prt_dec.loop2
#undef stack_prt_dec.reset
tmp_ret swap store #_ save return address _#
#_ set tmp_loop (loop-count) to unary 20 _#
tmp_loop swap 0xfffff store 
#label stack_prt_dec.loop1
stack_prt swap stack_prt load
swap stack_prt_dec.break swap
jumpif
swap stack_prt swap flip rot store #_ flip lowest bit of stack_prt _#
tmp_loop swap tmp_loop load rot store
swap stack_prt_dec.loop1 swap 
jumpif #_ jump to loop1 if rotation count not yet reached _#
swap stack_prt_dec.reset swap 
1 jumpif #_ jump to reset _#
#label stack_prt_dec.break
swap stack_prt swap flip store
#label stack_prt_dec.loop2
stack_prt swap stack_prt load rot store
tmp_loop swap tmp_loop load rot store
swap stack_prt_dec.loop2 swap 
jumpif #_ jump to loop2 if rotation count not yet reached _#
#label stack_prt_dec.reset
stack_prt swap stack_prt load
#_ rotate by 64-20 _#
rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot 
rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot rot 
rot rot rot rot rot rot rot rot rot rot rot rot   
store 
#undef stack_prt_dec.loop1
#undef stack_prt_dec.break
#undef stack_prt_dec.loop2
#undef stack_prt_dec.reset
tmp_ret load swap #_ load return address _#
1 jumpif
#_ end of stack_prt_dec _#


#def push 
swap stack_prt load swap store #_ store value at top of stack _#
stack_prt_dec swap 
1 jumpif #_ decrement the stack pointer _#
#enddef

#def pop 
stack_prt_inc swap 
1 jumpif #_ increment the stack_pointer _#
stack_prt load load #_ load value at top of stack _#
#enddef

#_ label for skipping code in file _#
#label $stack$.skipCode
#undef $stack$.skipCode